
<!DOCTYPE html>

<html style="height:100%;" >
<head>
<meta charset="utf-8"/>

<script src="webgl-utils.js"></script>
<script src="libgeom.js/mat4.js"></script>
<script src="libgeom.js/math.js"></script>
<script src="libgeom.js/mesh.js"></script>
<script src="libgeom.js/geom.js"></script>
<script src="libgeom.js/mouse.js"></script>
<script src="libgeom.js/camera.js"></script>
<script src="libgeom.js/select.js"></script>
<script src="render.js"></script>
<script src="main.js"></script>


<!--
<script src="demo.js"></script>
-->
<script id="cheapshader" type="x-shader/x-vertex">
uniform mat4 mvp_matrix;

attribute vec4 position;
attribute vec3 normal;
attribute vec4 color;

void main()
{
	gl_Position = mvp_matrix * position;
}
</script>


<script id="flipnormalshader" type="x-shader/x-vertex">
uniform mat4 mvp_matrix;
uniform mat4 normal_matrix;
uniform vec3 light0dir;
uniform vec3 light1dir;
uniform vec3 light2dir;
uniform float alt;
uniform vec4 altcolor;
uniform float altnormal;

attribute vec4 position;
attribute vec3 normal;
attribute vec4 color;

varying highp float v_Dot;
varying lowp vec4 v_Color;

void main()
{
	gl_Position = mvp_matrix * position;
	v_Color = (alt != 0.0 ? altcolor : color);
	vec4 transNormal = normal_matrix * vec4(normal*altnormal, 1);
	float l0dot = dot(transNormal.xyz, (normal_matrix * vec4(light0dir,1)).xyz);
	float l1dot = dot(transNormal.xyz, (normal_matrix * vec4(light1dir,1)).xyz);
	float l2dot = dot(transNormal.xyz, (normal_matrix * vec4(light2dir,1)).xyz);
	v_Dot = max(max(l0dot, 0.0) + max(l1dot, 0.0)+ max(l2dot, 0.0), 0.2);
}
</script>

<script id="passthrushader" type="x-shader/x-vertex">
attribute vec4 position;
attribute vec3 normal;
attribute vec4 color;
void main() {
	gl_Position = vec4(position);
}
</script>

<script id="peelshader" type="x-shader/x-fragment">
uniform highp sampler2D image0;
uniform highp float width;
uniform highp float height;

void main()
{
	highp vec2 korp = gl_FragCoord.xy * vec2(width, height);
	highp float fragz = gl_FragCoord.z;
	highp float texz = texture2D(image0, korp).x;
	if(fragz <= texz + 0.000001) //fudge factor for ipad;
		discard;
}
</script>

<script id="peelshader_nodiscard" type="x-shader/x-fragment">
#extension GL_EXT_frag_depth : enable
uniform highp sampler2D image0;
uniform highp float width;
uniform highp float height;
/*
varying highp float v_Dot;
varying lowp vec4 v_Color;
*/
void main()
{
	highp vec2 korp = gl_FragCoord.xy * vec2(width, height);
	highp float fragz = gl_FragCoord.z;
	highp float texz = texture2D(image0, korp).x;
//	if(fragz <= texz + 0.0000001) //fudge factor for ipad;
	if(fragz <= texz) //fudge factor for ipad;
		fragz = 1.0;
//		discard;
	gl_FragDepthEXT = fragz;
}
</script>

<script id="fillshader" type="x-shader/x-fragment">
uniform lowp vec4 fillcolor;
void main()
{
	gl_FragColor = fillcolor;
}
</script>


<script id="flatshader" type="x-shader/x-fragment">
precision highp float;
uniform highp sampler2D image0;
uniform highp float width;
uniform highp float height;
uniform lowp vec4 ambient;
varying highp float v_Dot;
varying lowp vec4 v_Color;

uniform highp float near;
uniform highp float far;


void main()
{

	gl_FragColor = vec4((v_Color.xyz) * v_Dot, v_Color.a);
	//gl_FragColor = vec4(amb,amb,amb,1.0)*v_Color.a;
// * 0.001*linear_z(texture2D(image0, korp).r);
}
</script>

<script id="ssaoshader" type="x-shader/x-fragment">
precision highp float;
uniform highp sampler2D image0;
uniform highp float width;
uniform highp float height;
uniform lowp vec4 ambient;
uniform lowp vec4 edgecolor;
varying highp float v_Dot;
varying lowp vec4 v_Color;

uniform highp float near;
uniform highp float far;

// from stackoverflow, @appas function they call it. I'd prefer a better one.
float snoise2(vec2 co){
    return fract(abs(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453));
}

float linear_z(float depth){
	float z_n = 2.0 * depth - 1.0;
	return 2.0 * near * far / (far + near - z_n * (far - near));
}

float silhouette(highp vec2 coord) {

	float a1 = linear_z(texture2D(image0, coord + vec2(0.0, -height)).x);
	float b0 = linear_z(texture2D(image0, coord + vec2(-width, 0.0)).x);
	float b2 = linear_z(texture2D(image0, coord + vec2(width, 0.0)).x);
	float c1 = linear_z(texture2D(image0, coord + vec2(0.0, height)).x);

	float a0 = linear_z(texture2D(image0, coord + vec2(-width, -height)).x);
	float a2 = linear_z(texture2D(image0, coord + vec2(width, -height)).x);
	float c0 = linear_z(texture2D(image0, coord + vec2(-width, height)).x);
	float c2 = linear_z(texture2D(image0, coord + vec2(width, height)).x);

	vec2 sobel;
	sobel.x = (a0+b0+b0+c0) - (a2+b2+b2+c2);
	sobel.y = (a0+a1+a1+a2) - (c0+c1+c1+c2);
	float len = length(sobel);

	len = max(len-100.0, 0.0);
//	len = max(len-30.0, 0.0);
//	len = max(len, 0.0);
	return min(len*len*len,1.0);
}

void main()
{
	highp vec2 korp = gl_FragCoord.xy * vec2(width, height);
	lowp float amb = 0.0;


	highp float zval = texture2D(image0, korp).r;
	const int nloops = 16;
	const float fudge = 0.05;
	lowp float dval = 1.0 / float(nloops);
	for(int j = 0; j < nloops; j++){
		highp float x = snoise2(vec2(gl_FragCoord.x*11.0+gl_FragCoord.y*5.0, j))-0.5;
		highp float y = snoise2(vec2(gl_FragCoord.x*x, float(j)*x))-0.5;
		highp float z = snoise2(vec2(gl_FragCoord.x*y, float(j)*y))-0.5;
		vec3 korpd = vec3(x,y,z);
		if(length(korpd) > 1.0)
			korpd = normalize(korpd);
		if(texture2D(image0, korp+fudge*korpd.xy).r > (zval+0.1*fudge*korpd.z))
			amb = amb + dval;
	}

	// clamp so that we never make anything brighter than it was already.
	amb = max(amb-0.5, 0.0);
/**/

	highp float silh = edgecolor.a*silhouette(korp);
	lowp vec3 colorvec = (1.0-silh) * v_Color.xyz + silh*edgecolor.xyz;
	lowp float alpha = v_Color.a;
	lowp float ambFact = 0.10;

	gl_FragColor = (1.0-ambFact)*vec4((colorvec) * v_Dot, alpha) + ambFact*vec4(amb,amb,amb,1.0);
}
</script>

<script id="restshader" type="x-shader/x-fragment">
uniform sampler2D image0;
uniform highp float width;
uniform highp float height;
varying highp float v_Dot;
varying lowp vec4 v_Color;
void main()
{
	highp vec2 korp = gl_FragCoord.xy * vec2(width, height);
	if(gl_FragCoord.z <= texture2D(image0, korp).r)
		discard;
	gl_FragColor = vec4(v_Color.xyz * v_Dot, v_Color.a);
}
</script>


<script id="stencilshader" type="x-shader/x-fragment">
uniform sampler2D image0;
uniform highp float width;
uniform highp float height;
//varying highp float v_Dot;
//varying lowp vec4 v_Color;
void main()
{
}
</script>



<script id="fxaashader" type="x-shader/x-fragment">
precision lowp float;
uniform sampler2D image0;
uniform float width;
uniform float height;
uniform lowp vec4 bgcolor;

/* from https://github.com/mitsuhiko/webgl-meincraft/blob/master/assets/shaders/fxaa.glsl */

#define FXAA_REDUCE_MIN   (1.0/ 128.0)
#define FXAA_REDUCE_MUL   (1.0 / 8.0)
#define FXAA_SPAN_MAX     8.0

vec3 blend(vec4 a, vec4 b){
	lowp float alpha = a[3];
	lowp float afact = alpha;
	lowp float bfact = 1.0 - alpha;
	return vec3(a[0]*afact+b[0]*bfact, a[1]*afact+b[1]*bfact, a[2]*afact+b[2]*bfact);
}

vec4 applyFXAA(vec2 fragCoord, sampler2D tex)
{
	vec4 color;
	vec2 inverseVP = vec2(width, height);
	vec3 rgbNW = blend(texture2D(tex, (fragCoord + vec2(-1.0, -1.0)) * inverseVP), bgcolor);
	vec3 rgbNE = blend(texture2D(tex, (fragCoord + vec2(1.0, -1.0)) * inverseVP), bgcolor);
	vec3 rgbSW = blend(texture2D(tex, (fragCoord + vec2(-1.0, 1.0)) * inverseVP), bgcolor);
	vec3 rgbSE = blend(texture2D(tex, (fragCoord + vec2(1.0, 1.0)) * inverseVP), bgcolor);
	vec3 rgbM  = blend(texture2D(tex, fragCoord  * inverseVP), bgcolor);
	vec3 luma = vec3(0.299, 0.587, 0.114);
	float lumaNW = dot(rgbNW, luma);
	float lumaNE = dot(rgbNE, luma);
	float lumaSW = dot(rgbSW, luma);
	float lumaSE = dot(rgbSE, luma);
	float lumaM  = dot(rgbM,  luma);
	float lumaMin = min(lumaM, min(min(lumaNW, lumaNE), min(lumaSW, lumaSE)));
	float lumaMax = max(lumaM, max(max(lumaNW, lumaNE), max(lumaSW, lumaSE)));

	vec2 dir;
	dir.x = -((lumaNW + lumaNE) - (lumaSW + lumaSE));
	dir.y =  ((lumaNW + lumaSW) - (lumaNE + lumaSE));

	float dirReduce = max((lumaNW + lumaNE + lumaSW + lumaSE) * (0.25 * FXAA_REDUCE_MUL), FXAA_REDUCE_MIN);

	float rcpDirMin = 1.0 / (min(abs(dir.x), abs(dir.y)) + dirReduce);
	dir = min(vec2(FXAA_SPAN_MAX, FXAA_SPAN_MAX),
		max(vec2(-FXAA_SPAN_MAX, -FXAA_SPAN_MAX),
		dir * rcpDirMin)) * inverseVP;

	vec3 rgbA = 0.5 * (
		blend(texture2D(tex, fragCoord * inverseVP + dir * (1.0 / 3.0 - 0.5)), bgcolor) +
		blend(texture2D(tex, fragCoord * inverseVP + dir * (2.0 / 3.0 - 0.5)), bgcolor)
	);
	vec3 rgbB = rgbA * 0.5 + 0.25 * (
		blend(texture2D(tex, fragCoord * inverseVP + dir * -0.5), bgcolor) +
		blend(texture2D(tex, fragCoord * inverseVP + dir * 0.5), bgcolor)
	);

	float lumaB = dot(rgbB.xyz, luma);
	if ((lumaB < lumaMin) || (lumaB > lumaMax))
		color = vec4(rgbA, 1.0);
	else
		color = vec4(rgbB, 1.0);
	return color;
}


void main() {
	vec4 color = applyFXAA(gl_FragCoord.xy, image0);
	gl_FragColor = color; 
}
</script>

<script id="copyshader" type="x-shader/x-fragment">
precision highp float;
uniform lowp vec4 bgcolor;
uniform sampler2D image0;
uniform float width;
uniform float height;

lowp vec3 blend(lowp vec4 a, lowp vec4 b){
	lowp float alpha = a[3];
	lowp float afact = alpha;
	lowp float bfact = 1.0 - alpha;
	return vec3(a[0]*afact+b[0]*bfact, a[1]*afact+b[1]*bfact, a[2]*afact+b[2]*bfact);
}

void main() {
	vec2 coord = gl_FragCoord.xy * vec2(width, height);
	vec4 color = texture2D(image0, coord);
	gl_FragColor = vec4(blend(color, bgcolor), 1.0);// + vec4(0.3, 0.3, 0.4, 0.0) * color.a;
}
</script>

<script id="fillshader" type="x-shader/x-fragment">
precision highp float;
uniform lowp vec4 fillcolor;
uniform float width;
uniform float height;

void main() {
	gl_FragColor = fillcolor;
}
</script>

</head>


<body onload="main()" style="height:100%; overflow:auto;">

<ul>
<li>Click-drag to move objects on a plane</li>
<li>Shift-drag to rotate camera around pivot</li>
<li>Drag-and-drop or use the button below to import binary stl files</li>
</ul>
<div style="float:left;">
	<input id="import-button" buttonText="import stl" type="file" name="files[]" multiple></input>
	<input id="ssao-enable" type="checkbox" checked="1">SSAO</input>
	<input id="fxaa-enable" type="checkbox"  checked="1">FXAA</input>
	fps:<span id="fps" >00.00</span>
	depth:<span id="depth" >0</span>
</div>

<div id="drop-area" style="width:100%;height:85%;">
	<canvas id="glcanvas" style="width:100%;height:100%;">
		It appears your browser has no canvas support, try using a different browser
	</canvas>
</div>

<div id="error" style="background-color: white; color:darkred; height:0;"></div>

</body>
</html>
